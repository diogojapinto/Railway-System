class Block is subclass of TrackElement
types
values
instance variables
	public train: [Railway`Train] := nil;
operations
	-- only one train per block
	public placeTrain: Railway`Train ==> ()
	placeTrain(newTrain) == (
		train := newTrain;
	)
	pre
		train = nil
	post
		train <> nil;
		
	public removeTrain: Railway`Train ==> ()
	removeTrain(trainVar) == 
		train := nil
	pre
		train = trainVar;
	
	public hasAnyTrain: () ==> bool
	hasAnyTrain() ==
		return train <> nil;
		
	public hasTrain: Railway`Train ==> bool
	hasTrain(trainVar) ==
		return train = trainVar;
	
	public initSemaphores: () ==> ()
	initSemaphores() ==
		if hasAnyTrain() then (
			setCSemaphore(<RED>);
			setCCSemaphore(<RED>);
		)
		else (
			if nextClockwise.hasAnyTrain() or isofclass(Station, nextClockwise) then
				setCSemaphore(<YELLOW>)
			else 
				setCSemaphore(<GREEN>);
				
			if nextCounterClockwise.hasAnyTrain() or isofclass(Station, nextCounterClockwise) then
				setCCSemaphore(<YELLOW>)
			else 
				setCCSemaphore(<GREEN>)
		)
		
functions
traces
end Block