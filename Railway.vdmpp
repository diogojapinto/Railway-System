class Railway
types
	public Direction = <CLOCKWISE> | <COUNTER_CLOCKWISE>;
	public Train :: id  : token
									dir : Direction;
values
instance variables
	public static trainCounter: nat := 0;
	
	private isClosed: bool := false;
	
	public trackElements: seq of TrackElement := [];
	private trains: set of Train := {};
operations

	--------------------
	--   Build ops    --
	--------------------
	
	public appendTrackElement: TrackElement ==> ()
	appendTrackElement(newTrackElem) == (
		if len trackElements > 0 then (
			-- set elements ordering
			newTrackElem.nextCounterClockwise := trackElements(len trackElements);
			trackElements(len trackElements).nextClockwise := newTrackElem;
		);
		
		-- add to sequence
		trackElements := trackElements ^ [newTrackElem];
	)
	pre
		len trackElements > 0 and isofclass(Station, newTrackElem) => not isofclass(Station, trackElements(len trackElements))
	post
		len trackElements = (len trackElements~ + 1) and
		len trackElements > 1 => trackElements(len trackElements - 1).nextClockwise = trackElements(len trackElements) and
		len trackElements > 1 => trackElements(len trackElements).nextCounterClockwise = trackElements(len trackElements - 1);
		
	public closeTrack: () ==> seq of TrackElement
	closeTrack() == (
		trackElements(len trackElements).nextClockwise := trackElements(1);
		trackElements(1).nextCounterClockwise := trackElements(len trackElements);
		isClosed := true;
		return trackElements;
	)
	post
		trackElements(1).nextCounterClockwise = trackElements(len trackElements) and
		trackElements(len trackElements).nextClockwise = trackElements(1);
	
	public placeTrain: nat * Direction ==> ()
	placeTrain(index, dir) == (
		dcl newTrain: Train := mk_Train(mk_token(trainCounter), dir);
		trainCounter := trainCounter + 1;
		
		trackElements(index).placeTrain(newTrain);
	);
	
functions
traces
end Railway