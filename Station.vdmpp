class Station is subclass of TrackElement
types
values
instance variables
	-- only one train in each parallel block
	public clockwiseTrain: [Railway`Train] := nil;
	public counterClockwiseTrain: [Railway`Train] := nil;
	
	-- must be followed and preceded by Blocks
	inv nextClockwise <> nil => isofclass(Block, nextClockwise);
	inv nextCounterClockwise <> nil => isofclass(Block, nextCounterClockwise)
	
operations
	-- place a train and set semaphores accordingly
	public placeTrain: Railway`Train ==> ()
	placeTrain(train) == (
		if train.dir = <CLOCKWISE> then (
			clockwiseTrain := train;
			setCSemaphore(<RED>);
		)
		else (
			counterClockwiseTrain := train;
			setCCSemaphore(<RED>);
		)
	)
	post
		-- trains circulate through the left
		(clockwiseTrain <> nil => clockwiseTrain.dir = <CLOCKWISE>)
		and (counterClockwiseTrain <> nil => counterClockwiseTrain.dir = <COUNTER_CLOCKWISE>);
		
    -- remove a train and set semaphores accordingly
	public removeTrain: Railway`Train ==> ()
	removeTrain(train) == 
		if clockwiseTrain = train then (
			clockwiseTrain := nil;
			if nextClockwise.isSemaphoreRed(<CLOCKWISE>) then
				setCSemaphore(<YELLOW>);
			-- semaphores in stations never get green
		)
		else (-- if counterClockwiseTrain = train then
			counterClockwiseTrain := nil;
			if nextCounterClockwise.isSemaphoreRed(<COUNTER_CLOCKWISE>) then
				setCCSemaphore(<YELLOW>)
			-- semaphores in stations never get green
		)
	pre
			-- train exists in sets of trains
		train in set {clockwiseTrain, counterClockwiseTrain};
	
	-- checks if Ctrain or CCtrain are not nil
	public hasAnyTrain: () ==> bool
	hasAnyTrain() ==
		return clockwiseTrain <> nil or counterClockwiseTrain <> nil;
		
	-- checks if it is a Ctrain or a CCtrain
	public hasTrain: Railway`Train ==> bool
	hasTrain(train) ==
		return clockwiseTrain = train or counterClockwiseTrain = train;
		
	-- checks if Ctrain is not nil	
	public hasCTrain: () ==> bool
	hasCTrain() ==
		return clockwiseTrain <> nil;
	
	-- checks if CCtrain is not nil	
	public hasCCTrain: () ==> bool
	hasCCTrain() ==
		return counterClockwiseTrain <> nil;
	
	-- Initialize semaphores of the track
	public initSemaphores: () ==> ()
	initSemaphores() == (
		-- semaphores never get red nor green initially, as there are no trains
		setCSemaphore(<YELLOW>);
		setCCSemaphore(<YELLOW>);
	)
functions		
traces
end Station